plugins {
    id 'com.android.application'
    id 'jacoco'
    id "org.sonarqube" version "3.1.1"
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.se2.bopit"
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        android.defaultConfig.vectorDrawables.useSupportLibrary = true
        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    testOptions {
        unitTests {
            includeAndroidResources = true

            /*
              this config helps us to avoid errors in unit tests like "Method d in android.util.Log not mocked";
               that is, we can run unit test on classes using android libraries,
               however, we will not see if we have forgot to mock some api,
               thus, deactivate it temporarily if you encounter weired errors in unit tests!
            */
            returnDefaultValues = true
        }
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'com.google.android.material:material:1.3.0'
    //    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "com.google.android.gms:play-services-games:21.0.0"
    implementation "com.google.android.gms:play-services-auth:19.0.0"
    implementation "com.google.android.gms:play-services-nearby:17.0.0"
    implementation 'com.google.code.gson:gson:2.8.6'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:3.9.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:3.3.0"
    androidTestImplementation 'org.awaitility:awaitility:4.0.3'
    implementation 'com.github.javafaker:javafaker:1.0.2'

    // https://mvnrepository.com/artifact/com.esotericsoftware/kryonet
    implementation 'com.esotericsoftware:kryonet:2.22.0-RC1'
    implementation "androidx.fragment:fragment:1.3.4"
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.16'
    implementation 'com.github.jd-alexander:android-flat-button:1.1'

    //websockets
    implementation('io.crossbar.autobahn:autobahn-android:20.7.1') {
        exclude group: 'com.github.joshjdevl.libsodiumjni', module: 'libsodium-jni-aar'
    }
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}


task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest','createDebugCoverageReport']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "${buildDir}/intermediates/javac/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories.from = files([mainSrc])
    classDirectories.from = files([debugTree])
    executionData.from = fileTree(dir: project.buildDir, includes: [
            "jacoco/testDebugUnitTest.exec",
            "outputs/code_coverage/debugAndroidTest/connected/**/*.ec"
    ])
}

task junitJacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest']) {
    description 'This task is a clone of jacocoTestReport adapted for local coverage reports on junit tests'

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "${buildDir}/intermediates/javac/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories.from = files([mainSrc])
    classDirectories.from = files([debugTree])
    executionData.from = fileTree(dir: project.buildDir, includes: [
            "jacoco/testDebugUnitTest.exec"
    ])
}

sonarqube {
    properties {
        property "sonar.projectKey", "Scodun_Bop-It"
        property "sonar.organization", "scodun-se2"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/reports/coverage/debug/report.xml"
    }
}

configurations.all {
    resolutionStrategy.dependencySubstitution {
        substitute module('org.hamcrest:hamcrest:2.1') with module('junit:junit:4.13.2')
    }
}